import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'com.gradleup.shadow' version '8.3.3'
}

group = 'dev.ezpadaz'
version = '1.0.2'

def baseVersion = '1.0.2'
def timestamp = new Date().format("yyyyMMddHHmm")
version = "${baseVersion}+${timestamp}"

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    maven { url 'https://jitpack.io' }

    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/" }
}

jar {
    destinationDirectory.set(file("server/plugins"))
}

shadowJar {
    archiveClassifier.set('')
    archiveFileName.set("VanillaPlus-${version}.jar")
    destinationDirectory.set(file("server/plugins"))

    dependencies {
        include(dependency('org.mongodb:mongodb-driver-sync:5.3.1'))
        include(dependency("co.aikar:acf-paper:0.5.1-SNAPSHOT"))
        include(dependency("com.elmakers.mine.bukkit:EffectLib:10.2"))
    }

    doFirst {
        def pluginDir = file("server/plugins")
        pluginDir.eachFileMatch(~/VanillaPlus-.*\.jar/) { it.delete() }
        println "ðŸ§¹ Cleaned old VanillaPlus jar(s) from plugins folder"
    }
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true

    def javaHome = System.getProperty("java.home")
    def javacPath = new File(javaHome, "bin/javac${System.properties['os.name'].toLowerCase().contains('win') ? '.exe' : ''}")
    options.forkOptions.executable = javacPath.absolutePath
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.5-R0.1-SNAPSHOT")
    compileOnly 'dev.aurelium:auraskills-api-bukkit:2.2.4'
    implementation 'org.mongodb:mongodb-driver-sync:5.3.1'
    implementation 'org.xerial:sqlite-jdbc:3.49.1.0'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'com.elmakers.mine.bukkit:EffectLib:10.2'
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"
    compileOnly 'dev.aurelium:auraskills-api-bukkit:2.2.4'
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks.withType(ShadowJar).configureEach {
    relocate 'co.aikar.commands', 'dev.ezpadaz.vanillaPlus.acf'
    relocate 'co.aikar.locales', 'dev.ezpadaz.vanillaPlus.locales'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}